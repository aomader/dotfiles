#!/bin/zsh

function precmd() { title }
function preexec() { title "$1" }

START_HOST="${HOST}"

function title() {
    local access
    local cmd

    if [[ $USERNAME != $LOGNAME || $START_HOST != $HOST ]]; then
        access="${USERNAME}@${HOST}: "
    fi

    if [[ $# -eq 0 ]]; then
        cmd="zsh %30<...<%~"
    else
        cmd="${(V)1//\%/\%\%}"
    fi

    case $TERM in
        rxvt*)
            print -Pn "\e]0;${access}${cmd}\e\\"
            ;;
        screen*)
            print -Pn "\ek${cmd}\e\\"
            print -Pn "\e_${access}\e\\"
            ;;
    esac
}

function git_branch() {
    git branch 2> /dev/null | grep '^\*' | sed 's/^\*\ //'
}

setopt prompt_subst
autoload -U colors && colors

GIT_CMD="git branch 2> /dev/null | sed -n 's/\* \(.*\)/\(\1\)/p'"
PROMPT="$fg[red]%(?..%? )%(#.$fg_bold[red].$fg[blue])%n$reset_color@$fg[green]%m$reset_color:$fg[cyan]%35<...<%~$reset_color %(#.#.%%) "

