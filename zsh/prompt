#!/bin/zsh

setopt prompt_subst

autoload -U colors && colors
autoload -Uz vcs_info

zstyle ':vcs_info:*' branchformat "%b"
zstyle ':vcs_info:*' actionformats "(%s/%b/%a)"
zstyle ':vcs_info:*' formats "(%s/%b)"
zstyle ':vcs_info:git:*' actionformats " %(%b/%a)"
zstyle ':vcs_info:git:*' formats "(%b)"

function title() {
    local access
    local cmd

    if [[ $USERNAME != $LOGNAME || -n $SSH_CLIENT ]]; then
        access="${USERNAME}@${HOST}: "
    fi

    if [[ $# -eq 0 ]]; then
        cmd="zsh %30<...<%~"
    else
        cmd="${(V)1//\%/\%\%}"
    fi

    case $TERM in
        rxvt*)
            print -Pn "\e]0;${access}${cmd}\e\\"
            ;;
        screen*)
            print -Pn "\ek${cmd}\e\\"
            print -Pn "\e_${access}\e\\"
            ;;
    esac
}

function vcs_pre() {
    PSVAR=""
    vcs_info
    [[ -n $vcs_info_msg_0_ ]] && PSVAR[1]="$vcs_info_msg_0_"
}

typeset -ga precmd_functions
typeset -ga preexec_functions

precmd_functions+='title'
precmd_functions+='vcs_pre'
preexec_functions+='title'

PROMPT='%(?..%{$fg[red]%}%? )%(#.%{$fg_bold[red]%}.%{$fg[blue]%})%n%{$fg_no_bold[default]%}@%{$fg[green]%}%m%{$fg[default]%}:%{$fg[cyan]%}%35<...<%~%(1v. %{$fg[yellow]%}%1v .)%{$fg[default]%}%(#.#.%%) '

